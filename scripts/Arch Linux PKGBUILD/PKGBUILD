# Maintainer: Clownacy <clownacy at yahoo dot com>
pkgname=clownmaped
pkgver=v1.2
pkgrel=1
pkgdesc='Mega Drive Sonic the Hedgehog sprite editor'
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url='https://github.com/Clownacy/ClownMapEd'
license=('AGPL-3.0-or-later' 'MIT' '0BSD' 'LGPL-3.0-or-later')
depends=('qt6-base')
makedepends=('cmake')
source=(
  "git+https://github.com/Clownacy/ClownMapEd.git#commit=1cf021c6d07f56b5444659fbf0d7c9a584bacb84"
  "git+https://github.com/gfiumara/CIEDE2000.git#commit=af1de42515f3916c16e75980a4635962489af56a"
  "git+https://github.com/Clownacy/libsonassmd.git#commit=3781df7aca6df1f473515b23f5433d74588b0e79"
  "git+https://github.com/MainMemory/FW-KENSC-ShellExt.git#commit=5f6464c6903a6b02ec095f45954df915fd54a59e"
  "git+https://github.com/Clownacy/clownassembler.git#commit=53542a8084b80452059364a014520e4b6688eb7d"
  "git+https://github.com/Clownacy/clowncommon.git#commit=37d1efd90725a7c30dce5f38ea14f1bc3c29a52f"
)
sha256sums=(
  '10682eb4a0d7dbb86f3706677d14c8ede41f0afa8bda2d1bb8f94714c67e6a4a'
  'ed94b6b04df35728fdbbe5176c3769fce07965ecb30f58a6d67f8d488972f2a7'
  '07f11e0db5652c9e5cf3bd78936d444656a89bb064391b41a844d8ba72c1acac'
  '09b7d06b39928a8126d128975d5de9a5da33b2ea4bfbb9910d6b5cbd4c5f6541'
  '81c1d37431d551b5db58e85d179e033288d5e8f3878ec3eab8f4d04e92a66a9a'
  '3889459af1aa96bf2d8be3ab9cf46387960283a2ecde833c24dfa5d4a217b69b'
)

prepare() {
  cd "ClownMapEd"
  git submodule init CIEDE2000 libsonassmd
  git config submodule.libs/CIEDE2000.url "$srcdir/CIEDE2000"
  git config submodule.libs/libsonassmd.url "$srcdir/libsonassmd"
  git -c protocol.file.allow=always submodule update

  cd "libsonassmd"
  git submodule init FW-KENSC-ShellExt clownassembler clowncommon
  git config submodule.libs/FW-KENSC-ShellExt.url "$srcdir/FW-KENSC-ShellExt"
  git config submodule.libs/clownassembler.url "$srcdir/clownassembler"
  git config submodule.libs/clowncommon.url "$srcdir/clowncommon"
  git -c protocol.file.allow=always submodule update

  cd "clownassembler"
  git submodule init clowncommon
  git config submodule.libs/clowncommon.url "$srcdir/clowncommon"
  git -c protocol.file.allow=always submodule update
}

build() {
  cmake -B build -S ClownMapEd \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
